Delta define: #True as: (
(Class subclassOf: 'Boolean' instanceVariables: '')) !

(Delta mirrorFor: #True) revision: '$Revision: 1.5 $'!

(Delta mirrorFor: #True) group: 'base'!

(Delta mirrorFor: #True)
comment: 
'Tags: %BlueBook

(c) 1995-1997 Sun Microsystems, Inc. ALL RIGHTS RESERVED. 
Use and distribution of this software is subject to the terms of the attached source license.   $Revision: 1.5 $
'!

! (Delta mirrorFor: #True) methodsFor: 'operations' !


& other <Boolean>	^<Boolean>

	^other!

not ^<Boolean>

	^false!

| other <Boolean>	^<Boolean>

	^true! !

! (Delta mirrorFor: #True) methodsFor: 'control' !


&& f <[^Boolean]>	^<Boolean>

	^f value!

and: f <[^Boolean]>	^<Boolean>

	^f value!

ifFalse: then <[]> ^<Object>

	^nil!

ifFalse: then <[^X def]> ifTrue: else <[^Y def]> ^<X|Y>

	^else value!

ifTrue: then <[]> ^<Object>

	^then value!

ifTrue: then <[^X def]> ifFalse: else <[^Y def]> ^<X|Y>

	^then value!

or: f <[^Boolean]>	^<Boolean>

	^true!

|| f <[^Boolean]>	^<Boolean>

	^true! !

! (Delta mirrorFor: #True) methodsFor: 'printing' !


printOn: strm <CharOutputStream>

	strm nextPutAll: 'true'! !

! (Delta mirrorFor: #True) methodsFor: 'converting' !


asExternalData ^<Int>

	^1! !

! (Delta mirrorFor: #True) methodsFor: 'utility' !


onFalse: blk <[^Object]>  ^<Boolean>

	^self!

onTrue: blk <[^Object]>  ^<Boolean>

	blk value.
	^self! !

