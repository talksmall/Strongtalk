Delta define: #WeakArray as: (
Generic forAll: 'E ' body: ((Class subclassOf: 'IndexedNextOfKinInstanceVariables[E] |> Array[E]' instanceVariables: 'dependents <NotifyingObject>') classVariables: 'FinalizationProcess <Process>
FinalizationSemaphore <Semaphore>')) !

(Delta mirrorFor: #WeakArray) revision: '$Revision: 1.5 $'!

(Delta mirrorFor: #WeakArray) group: 'base'!

(Delta mirrorFor: #WeakArray)
comment: 
'Implements Arrays whose elements are checked by the garbage collector
to see if their memory would be reclaimed if they weren''t referenced by a
WeakArray.  This is called a ''near-death experience''.   The weak array is notified
whenever such a reference is detected by the collector, with the message
hadNearDeathExperience:.  By default, this message just nils out any
references to the object, so that it can be reclaimed on the next garbage
collection. 

Compatibility note:
This kind of WeakArray is not quite the same as the WeakArrays implemented
in some other Smalltalks.  Other WeakArrays implement ''post-facto'' notification,
which some company has patented.  Our WeakArrays actually are more flexible,
since they implement notification before-the-fact of object collection, so that
the contents of the object are still accessible.  For example, if this was used to
hold references to open file descriptors, upon notification, the file descriptor can
be closed, and then its reference nilled out so that it can be collected.  With
previous WeakArrays, the descriptor gets collected before you find out that it
was destroyed, so you have to keep the information needed to close the file
in some additional data structure.

Tags: %User

(c) 1995-1997 Sun Microsystems, Inc. ALL RIGHTS RESERVED. 
Use and distribution of this software is subject to the terms of the attached source license
'!

! (Delta mirrorFor: #WeakArray) classSide methodsFor: 'initialization' !


initialize

	self startFinalizationProcess! !

! (Delta mirrorFor: #WeakArray) classSide methodsFor: 'instance creation' !


new: size <Int>

	self startFinalizationProcess.
	^(super new: size)
		initialize;
		yourself! !

! (Delta mirrorFor: #WeakArray) classSide methodsFor: 'private-finalization' !


checkNotification
	[ 
		{{primitiveNotificationQueueGetIfFail: [ :err |^self]}}  notify.
	] repeat.!

finalizationBlock

	^[[[self checkNotification]
			on: Error
			do: [:error| self handleFinalizationError: error] .
		self finalizationSemaphore wait] repeat]!

finalizationPriority

	"%TODO revisit once Process prioritization implemented"
	^Processor activePriority!

finalizationProcess

	^FinalizationProcess!

finalizationSemaphore

	self initFinalizationSemaphore.
	^FinalizationSemaphore!

handleFinalizationError: error

	FinalizationProcess := nil.
	error pass!

hasFinalizationProcess

	^self finalizationProcess notNil
		and: [self finalizationProcess running]!

initFinalizationSemaphore

	FinalizationSemaphore isNil
		ifTrue: [FinalizationSemaphore := Semaphore new]!

startFinalizationProcess

	self hasFinalizationProcess ifTrue: [^self].
	self terminateFinalizationProcess.
	self initFinalizationSemaphore.
	FinalizationProcess := self finalizationBlock forkAt: self finalizationPriority!

terminateFinalizationProcess

	true ifTrue: [^self]. "%TODO remove when stack overflow issue fixed"
	self finalizationProcess notNil
		ifTrue: [[self finalizationProcess terminate]
						on: Error
						do: [:ignore|]]! !

! (Delta mirrorFor: #WeakArray) classSide methodsFor: 'restricted to VM-finalization' !


signalFinalization

	self finalizationSemaphore waitWouldBlock
		ifTrue: [self finalizationSemaphore signal]! !

! (Delta mirrorFor: #WeakArray) methodsFor: 'notification' !


hadNearDeathExperience: anObject
	^{{primitiveHadNearDeathExperience: anObject}}!

notify
	1 to: self size do: [ :index |
		| victim |
		(self hadNearDeathExperience: (victim := self at: index)) ifTrue: [
			self at: index put: nil.
			dependents changed: #finalize with: victim
		]
	]! !

! (Delta mirrorFor: #WeakArray) methodsFor: 'private-initialization' !


initialize

	dependents := NotifyingObject new! !

! (Delta mirrorFor: #WeakArray) methodsFor: 'weak dependents' !


addWeakDependent: dependent

	dependents addDependent: dependent!

removeWeakDependent: dependent

	dependents removeDependent: dependent! !

